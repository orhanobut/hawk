apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

apply plugin: 'checkstyle'

check.dependsOn('checkstyle')

task checkstyle(type: Checkstyle) {
  configFile = rootProject.file('checkstyle.xml')
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

android {
  compileSdkVersion 21
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 21
  }

  lintOptions {
    abortOnError false
  }
}

// build a jar with source files
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError  false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

configurations {
  optional
  compile.extendsFrom optional
}

dependencies {
  compile 'com.google.code.gson:gson:2.3.1'
  optional 'io.reactivex:rxandroid:1.0.1'

  testCompile 'junit:junit:4.12'
  testCompile "org.mockito:mockito-core:1.9.5"
  testCompile "org.robolectric:robolectric:3.0"
  testCompile 'org.assertj:assertj-core:1.7.0'
}