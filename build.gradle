// Top-level build file where you can add configuration options common to all sub-projects/modules.
subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'checkstyle'

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }

  buildscript {
    repositories {
      jcenter()
      google()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:3.0.1'
    }
  }

  repositories {
    jcenter()
    google()
  }
}

ext {
  minSdkVersion = 14
  targetSdkVersion = 27
  compileSdkVersion = 27
  buildToolsVersion = '27.0.3'

  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
        gson          : 'com.google.code.gson:gson:2.8.2',

        // Test dependencies
        junit         : 'junit:junit:4.12',
        truth         : 'com.google.truth:truth:0.39',
        robolectric   : 'org.robolectric:robolectric:3.7.1',
        mockito       : 'org.mockito:mockito-core:1.10.19',
        espressoRunner: 'com.android.support.test:runner:0.5',
        espressoRules : 'com.android.support.test:rules:0.5'
]
