// Top-level build file where you can add configuration options common to all sub-projects/modules.
subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'checkstyle'

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }

  buildscript {
    repositories {
      mavenCentral()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:2.1.3'
    }
  }

  repositories {
    jcenter()
  }
}

ext {
  minSdkVersion = 10
  targetSdkVersion = 24
  compileSdkVersion = 24
  buildToolsVersion = '24.0.2'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
    gson          : 'com.google.code.gson:gson:2.4',
    conceal       : 'com.facebook.conceal:conceal:1.1.3@aar',

    // Test dependencies
    junit         : 'junit:junit:4.12',
    truth         : 'com.google.truth:truth:0.28',
    robolectric   : 'org.robolectric:robolectric:3.1',
    mockito       : 'org.mockito:mockito-core:1.10.19',
    espressoRunner: 'com.android.support.test:runner:0.5',
    espressoRules : 'com.android.support.test:rules:0.5'
]
